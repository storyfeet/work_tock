//helper
WHITESPACE = _{" "|"\t"}
COMMENT = _{"#" ~ (!(","|"\n") ~ ANY)*}
Num = {"-"? ~ ASCII_DIGIT+ }

StrInner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

Word={(LETTER|NUMBER)+}

//core parts 

Plusnum={ASCII_DIGIT+}
Time=${Plusnum ~ ":" ~ Plusnum}
Clockout=${"-" ~ Time}
ClockIO={ Time ~ "-" ~ Time} 
Date = ${Plusnum ~ "/" ~ Plusnum ~ ("/" ~ Plusnum)?}

NumSetter = {("="~Word ~ ":" ~ Num) | (Word ~ "=" ~ Num )}
Tag = ${"_"~ Word}
ClearTags = ${"__" ~ Word?}
Job = {("\"" ~ StrInner ~ "\"") | Word}

//outer
Record = {( ClockIO| Time | Clockout | Date |NumSetter | ClearTags| Tag| Job)}

Main = {SOI ~ Record? ~ ((","|"\n")+ ~ Record)* ~ (","|"\n")? ~ EOI }



